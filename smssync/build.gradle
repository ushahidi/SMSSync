apply plugin: 'android'

repositories {
    mavenCentral()
}

dependencies {
    compile project(':libraries:pay-me')
    compile fileTree(dir: 'libs', include: '*.jar')
    compile 'com.actionbarsherlock:actionbarsherlock:4.4.0@aar'
    compile 'com.android.support:support-v4:18.0.+'
    compile 'com.squareup:otto:1.3.4'
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.1"

    sourceSets {

        main {
            manifest.srcFile 'src/main/AndroidManifest.xml'
            java.srcDirs = ['src/main/java']
            res.srcDirs = ['src/main/res']
        }

        defaultConfig {
            testPackageName "org.addhen.smssync.tests"
            packageName "org.addhen.smssync"
        }

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    signingConfigs {

        releaseSign
    }

    buildTypes {

        proguard.initWith(buildTypes.debug)
        proguard {
            runProguard true
            proguardFile getDefaultProguardFile('proguard-android.txt')
        }

        release {
            signingConfig signingConfigs.releaseSign
        }

        debug {
            packageNameSuffix ".debug"
            versionNameSuffix "-DEBUG"
        }

        proguard.initWith(buildTypes.release)
        proguard {
            runProguard true
            proguardFile getDefaultProguardFile('proguard-android.txt')
        }

    }

    defaultConfig {
        versionCode 20
        versionName "2.5.1"
        minSdkVersion 8
        targetSdkVersion 19
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    android.applicationVariants.all { variant ->
        // add google analytics code here. This is defined in the gradle.properties file
        variant.mergeResources.doLast {
            if (project.hasProperty('googleAnalyticsCode')) {
                File valuesFile =
                        file("${buildDir}/res/all/${variant.dirName}/values/values.xml")
                def analyticsCode = "<string name=\"ga_trackingId\">${googleAnalyticsCode}</string>"

                String content = valuesFile.getText('UTF-8')
                content = content.replaceAll("<string name=\"ga_trackingId\"></string>",
                        analyticsCode)
                valuesFile.write(content, 'UTF-8')
            }
        }

        def apk = variant.outputFile;

        def newName = apk.name.replace(".apk",
                "-v" + defaultConfig.versionName + "-" + variant.buildType.name.toUpperCase() +
                        ".apk");

        variant.outputFile = file("$project.buildDir/apk/" + newName);
        if (variant.zipAlign) {
            variant.zipAlign.outputFile =
                    file("$project.buildDir/apk/" + newName.replace("-unaligned", ""));
        }
    }

    flavorGroups "analytics"
    productFlavors {

        withAnalytics {
            flavorGroup "analytics"
        }

        noAnalytics {
            flavorGroup "analytics"
        }
    }
}

if (project.hasProperty('releaseKeyStore') &&
        project.hasProperty('releaseKeyPassword') &&
        project.hasProperty('releaseKeyStorePassword') &&
        project.hasProperty('releaseKeyAlias')) {
    android.signingConfigs.releaseSign.keyAlias = releaseKeyAlias
    android.signingConfigs.releaseSign.storeFile = file(releaseKeyStore)
    android.signingConfigs.releaseSign.storePassword = releaseKeyStorePassword
    android.signingConfigs.releaseSign.keyPassword = releaseKeyPassword
} else {
    android.signingConfigs.releaseSign.keyAlias ""
    android.signingConfigs.releaseSign.storeFile null
    android.signingConfigs.releaseSign.storePassword ""
    android.signingConfigs.releaseSign.keyPassword ""
}



